// https://github.com/oculus-samples/Unity-DepthAPI/issues/16
#include "../Depth/DepthKit.hlsl"
#include "../Environment/Environment.hlsl"

#pragma kernel Accumulate // 0
#pragma kernel Apply      // 1
#pragma kernel Init       // 2

RWStructuredBuffer<int> _PerFrameHeight; // should be 32bit signed int
RWTexture2D<float2> _EnvHeightMapWritable;
//RWTexture2D<float2> _WallFieldWritable; 

uint _TexSize;
uint _DepthSamples;

float2 _DepthRange;
float2 _HeightRange;

float3 _DepthFramePos;

float _EdgeFilterSize;
float _GradientCutoff;

float _LerpHeight;

float _WallMinHeight;

#define HEIGHT_INT_FACTOR 100
#define PER_FRAME_UNWRITTEN 0

#define VERTI uint2(0, 1)
#define HORIZ uint2(1, 0)

uint CoordToIndex(uint2 coord)
{
	return coord.y * _TexSize + coord.x;
}

bool WorldWithinFrame(float3 world)
{
	float2 ndc = agDepthWorldToNDC(world).xy;
	return all(ndc.xy > float2(0, 0)) && all(ndc.xy < float2(1, 1));
}

float IntToFloatHeight(int height)
{
	return float(height) / HEIGHT_INT_FACTOR;
}

int FloatToIntHeight(float height) 
{
	return height * HEIGHT_INT_FACTOR;
}

[numthreads(64, 1, 1)]
void Accumulate(uint3 id : SV_DispatchThreadID)
{
	float2 depthUV = float2(id.xy) / _DepthSamples;
	float depth = saturate(agDepthSample(depthUV));
	float3 depthWorld = agDepthNDCtoWorld(float3(depthUV, depth));
	float height = depthWorld.y;
	int intHeight = FloatToIntHeight(depthWorld.y);
	
	float2 uv = saturate(agEnvWorldToUV(depthWorld));
    uint2 mapCoord = uint2(uv * _TexSize); //round(uv * _TexSize);
	uint index = CoordToIndex(mapCoord);
	
	// within valid height range
	float2 heightRangeWorld = _HeightRange + _DepthFramePos.y;
	bool validY = (heightRangeWorld.x < height) && (height < heightRangeWorld.y);
	
	// within valid distance
	float dist = distance(depthWorld.xz, _DepthFramePos.xz);
	bool validDistance = (_DepthRange.x < dist) && (dist < _DepthRange.y);
	
	// previous height is visible in view
    float2 heightMapVal = _EnvHeightMapWritable[mapCoord];
    float prevHeight = heightMapVal.r;
    bool prevWritten = heightMapVal.g != 0;
    float3 prevWorldPos = float3(depthWorld.x, prevHeight, depthWorld.z);
    bool prevHeightInView = WorldWithinFrame(prevWorldPos) || !prevWritten;
	
	// sobel outline - need to optimize 
    float dl = agDepthNDCToLinear(depth);
    float dlL = agDepthNDCToLinear(agDepthSample(depthUV + float2(_EdgeFilterSize, 0)));
    float dlR = agDepthNDCToLinear(agDepthSample(depthUV - float2(_EdgeFilterSize, 0)));
    float dlU = agDepthNDCToLinear(agDepthSample(depthUV + float2(0, _EdgeFilterSize)));
    float dlD = agDepthNDCToLinear(agDepthSample(depthUV - float2(0, _EdgeFilterSize)));
    bool notEdge = (abs(dlL - dlR) < _GradientCutoff) && (abs(dlD - dlU) < _GradientCutoff);
	
	bool valid = validY && validDistance && notEdge && prevHeightInView;
	
	if (!valid)
		return;
	
	InterlockedMax(_PerFrameHeight[index], intHeight);
}

[numthreads(8, 8, 1)]
void Apply(uint3 id : SV_DispatchThreadID)
{
	uint index = CoordToIndex(id.xy);
	int intHeight = _PerFrameHeight[index];
	
    bool wasWritten = intHeight != PER_FRAME_UNWRITTEN;
	
	float2 heightMapVal = _EnvHeightMapWritable[id.xy]; // r = height, g = is written
	float r = IntToFloatHeight(intHeight);
	bool g = heightMapVal.g || wasWritten;
    
	_PerFrameHeight[index] = PER_FRAME_UNWRITTEN;
	
	if(!wasWritten)
		return;
	
    _EnvHeightMapWritable[id.xy] = float2(r, g);
}

[numthreads(8, 8, 1)]
void Init(uint3 id : SV_DispatchThreadID)
{
	uint index = CoordToIndex(id.xy);
    _PerFrameHeight[index] = PER_FRAME_UNWRITTEN;
	_EnvHeightMapWritable[id.xy] = 0;
	//_WallFieldWritable[id.xy] = 0;
}

//[numthreads(8, 8, 1)]
//void Walls(uint3 id : SV_DispatchThreadID)
//{	
//    uint2 id2 = uint2(clamp(id.x, 0, _TexSize - 1), clamp(id.y, 0, _TexSize - 1));
//    float diff = abs(_EnvHeightMapWritable[id2] - _EnvHeightMapWritable[id2 + VERTI]) +
//		abs(_EnvHeightMapWritable[id2] - _EnvHeightMapWritable[id2 + HORIZ]);

//    _WallFieldWritable[id.xy] = lerp(0, 1, diff > _WallMinHeight);
	
	
//}

//[numthreads(8, 8, 1)]
//void WallsSDF(uint3 id : SV_DispatchThreadID)
//{
	
//}